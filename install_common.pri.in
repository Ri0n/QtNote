QT *= core

greaterThan(QT_MAJOR_VERSION, 4) {
	CONFIG += c++11
} else {
	QMAKE_CXXFLAGS += -std=c++11
}

APPNAME = $$APPNAME
DEFINES += APPNAME=\\\"$$APPNAME\\\"

VERSION = $$VERSION

DEFINES += QTNOTE_VERSION_S=$$VERSION
DEFINES += QTNOTE_VERSION_W=$$join(VERSIONS_FULL, ",")
DEFINES += QTNOTE_VERSION_MAJOR=$$member(VERSIONS_FULL, 0)
DEFINES += QTNOTE_VERSION_MINOR=$$member(VERSIONS_FULL, 1)
DEFINES += QTNOTE_VERSION_PATCH=$$member(VERSIONS_FULL, 2)

unix {
	PREFIX = $$PREFIX
	DATADIR = $$PREFIX/share
	LIBDIR = $$PREFIX/lib
	MANDIR = $$DATADIR/man/man1
	TRANSLATIONSDIR = $$DATADIR/$$APPNAME

	DEFINES += LIBDIR=\\\"$$LIBDIR\\\" \
		DATADIR=\\\"$$DATADIR\\\" \
		TRANSLATIONSDIR=\\\"$$TRANSLATIONSDIR\\\"
}
